<block> ::= <statement> <block> 
          | ''

<block_no_loop> ::= <statement_no_loop> <block_no_loop>
              | ''

<statement> ::= 'if' '(' <expression> ')' '{' <block> '}' else '{' <block> '}'
              | 'if' '(' <expression> ')' '{' <block> '}'
              | 'iterations = 0;' 'while' '(' <expression> ' && iterations < s->frame_count' ')' '{' <block_no_loop> 'iterations++;' '}'
              | <modifiable> '=' <term> ';'
              | 's->write' '(' <info_field_index> ',' <term> ',' <term> ')' ';'
              | 's->set_accessed' '(' <term> ',' <term> ')' ';'

<statement_no_loop> ::= 'if' '(' <expression> ')' '{' <block_no_loop> '}' else '{' <block_no_loop> '}'
                  | 'if' '(' <expression> ')' '{' <block_no_loop> '}'
                  | <modifiable> '=' <term> ';'
                  | 's->write' '(' <info_field_index> ',' <term> ',' <term> ')' ';'
                  | 's->set_accessed' '(' <term> ',' <term> ')' ';'

<expression> ::= <term>
               | <term> '==' <term>
               | <term> '!=' <term>
               | <term> '>' <term>
               | <term> '>=' <term>
               | <term> '<' <term>
               | <term> '<=' <term>
               | <term> '+' <term>
               | <term> '-' <term>
               | <term> '*' <term>
               | 's->division' '(' <term> ',' <term> ')'
               | 's->remainder' '(' <term> ',' <term> ')'

<term> ::= <modifiable>
         | <non_modifiable>
         | <expression>

<modifiable> ::= 'frame'
               | 's->num1'
               | 's->num2'
               | 's->num3'

<non_modifiable> ::= 's->time'
                   | 's->cache_size'
                   | 's->page_request'
                   | 's->find_min' '(' <info_field_index> ')'
                   | 's->find_max' '(' <info_field_index> ')'
                   | 's->read' '(' <info_field_index> ',' <term> ')'
                   | 's->page_access_count_min' '(' ')'
                   | 's->page_access_count_max' '(' ')'
                   | 's->last_accessed_min' '(' ')'
                   | 's->last_accessed_max' '(' ')'
                   | 's->added_to_cache_min' '(' ')'
                   | 's->added_to_cache_max' '(' ')'
                   | 's->get_accessed' '(' <term> ')'
                   | 's->page_access_count_min' '(' ')'
                   | <number>
                   | <bool>

<bool> ::= 'true'
         | 'false'

<number> ::= <first_digit> <tail_digits> | '0'

<first_digit> ::= '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'

<tail_digits> ::= <digit> <tail_digits> | '' 

<digit> ::= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'

<info_field_index> ::= '1' | '2'